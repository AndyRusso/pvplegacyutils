// This file is copied and modified from the Shared Resources mod, with permission by the creators: enjarai and jacg.
// Their repository: https://github.com/enjarai/shared-resources
// Copied file: https://github.com/enjarai/shared-resources/blob/master/build.gradle

plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
}

loom {
    // im not sure what this does,
    // but i think we might need it
    runConfigs.configureEach {
        ideConfigGenerated = true
    }

    accessWidenerPath = file("src/main/resources/pvplegacyutils.accesswidener")
}

// stuff that applies to all projects
allprojects {
    apply plugin: "maven-publish"
    apply plugin: "fabric-loom"

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    java {
        withSourcesJar()
    }

    version = project.mod_version
    group = project.maven_group

    // add repos for all projects
    repositories {
        maven {
            name "Terraformers Maven"
            url 'https://maven.terraformersmc.com'
        }
        maven {
            name "Shedaniel Maven"
            url "https://maven.shedaniel.me/"
        }
    }

    // floader needs to be in every sub module
    dependencies {
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    }

    processResources {
        inputs.property "version", version

        filesMatching("fabric.mod.json") {
            expand "version": version
        }
    }
}

dependencies {
    // dummy versions
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modCompileOnly "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
    modCompileOnly("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    subprojects.forEach((project) -> {
        include(project) {
            transitive = false
        }
    })
}

// stuff that applies to all sub projects
subprojects {
    dependencies {
        // include the root project because that's where our common code is
        implementation(project(path: ":", configuration: "namedElements")) {
            // having 2 floaders from the sub project and root project causes issues so we get rid of one.
            exclude group: "net.fabricmc", module: "fabric-loader"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}